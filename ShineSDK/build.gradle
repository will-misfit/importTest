apply plugin: 'com.android.library'
apply plugin: 'maven'


def shineSDKVersionName = "2.3.4"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultPublishConfig "developRelease"
    publishNonDefault true

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22
        versionCode 28
        versionName shineSDKVersionName
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    // source: http://ph0b.com/android-studio-gradle-and-ndk-integration/
    // call regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        def ndkDir = project.plugins.getPlugin('com.android.library').sdkHandler.getNdkFolder().toString()
        def ndkDirPath = ndkDir + '/ndk-build'
        commandLine ndkDirPath, '-C', file('src/main').absolutePath
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    buildTypes {
        def API_SERVER = "API_SERVER"

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", API_SERVER, "\"https://data-again.eng.int.misfit.com/v3/events\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", API_SERVER, "\"https://data.misfit.com/v3/events\""
        }
    }
    productFlavors {
        def ACCESS_KEY = "ACCESS_KEY"

        develop {
            buildConfigField "String", ACCESS_KEY, "\"92837495897234985-giP2z8d5TJ6nO2G9ZdDvzJg5391JHDG1\""
        }
        misfit {
            buildConfigField "String", ACCESS_KEY, "\"23209348793248639-dulJD6O48s6l6K6A605KC0WnNkh05Q8R\""
        }
    }
}

dependencies {
    provided fileTree(dir: 'libs_reference', include: ['*.jar'])
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.+'
    testCompile 'junit:junit:4.12'
}

uploadArchives {
    repositories.mavenDeployer {
        String repoPath = projectDir.absolutePath + "/mavenRepo";
        File mavenRepoDir = new File(projectDir.absolutePath + "/mavenRepo")
        if (!mavenRepoDir.isDirectory()) {
            mavenRepoDir.mkdirs()
        }

        String repoUri = mavenRepoDir.toURI().toString();
        repository(url: repoUri)

        project.version
        pom.groupId = "misfit.com"
        pom.artifactId = "ShineSDK"
        pom.version = shineSDKVersionName
    }
}